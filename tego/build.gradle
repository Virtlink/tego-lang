plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm'  version '1.5.0' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.5.0' apply false
}

configure(allprojects.findAll {it.name != 'tego-platform'}) {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'

    group 'com.virtlink'
    version '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation      platform(project(":tego-platform"))
        testImplementation  platform(project(":tego-platform"))
        testRuntimeOnly     platform(project(":tego-platform"))

        // Platform
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"

        // Logging
        implementation "org.slf4j:slf4j-api"
        implementation "org.slf4j:slf4j-simple"
        implementation "io.github.microutils:kotlin-logging-jvm"

        // Testing
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ['-Xjvm-default=all', '-Xuse-ir']
        }
    }
    test {
        useJUnitPlatform()
    }
    kapt {
        correctErrorTypes true
    }
}
