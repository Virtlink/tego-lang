module statics

imports
  signatures/Tego-sig

// Single-file entry point
rules programOk : Module

  programOk(module) :- {s}
    new s,
    init(s),
    moduleOk(s, module).

// Multi-file entry point
rules projectOk : scope

  projectOk(s) :-
    init(s).

rules fileOk : scope * Module

  fileOk(s, module) :- moduleOk(s, module).

rules init : scope

  init(s) :- true.

rules moduleOk : scope * Module

  moduleOk(s, Module(moduleDecl, decls)) :- {ms}
    new ms, ms -P-> s,
    moduleDeclOk(s, moduleDecl, ms),
    declsOk(ms, decls).

  moduleOk(s, Module-Plhdr()).

rules moduleDeclOk : scope * ModuleDecl * scope

  moduleDeclOk(s, ModuleDecl(name), ms) :-
    declareModule(s, name, ms).

  moduleDeclOk(s, ModuleDecl-Plhdr(), ms).

rules declOk : scope * Decl
  declsOk maps declOk(*, list(*))

  declOk(s, d@ValDef(name, type, body)) :- {T TBODY}
    typeOfType(s, type) == T,
    typeOfExp(s, body) == TBODY,
    subtypeOf(TBODY, T),
    declareDecl(s, name, T),
    @d.type := T.

  declOk(s, d@StrategyDecl(mods, name, typeParams, params, inType, outType)) :- {ds TPs T PTs IT OT}
    // Create strategy declaration scope in which the type variables are declared
    new ds, ds -P-> s,
    typesOfTypeParamDecls(ds, typeParams) == TPs,
    typesOfTypes(ds, params) == PTs,
    typeOfType(ds, inType) == IT,
    typeOfType(ds, outType) == OT,
    T == GENERIC(TPs, STRATEGY(PTs, IT, OT)),
    declareDecl(s, name, T),
    @d.type := T.

  declOk(s, d@StrategyDef(name, paramDefs, body)) :- {T PTs IT OT ss}
    typeOfType(s, TypeRef(name)) == T@STRATEGY(PTs, IT, OT),
    new ss, ss -P-> s,
    paramDefsOk(ss, paramDefs, PTs),
    typeOfExp(ss, body) == STRATEGY([], IT, OT),
    @d.type := T.

  declOk(s, d@RuleDef(name, paramDefs, inExp, outExp)) :- {T PTs IT OT ss}
    typeOfType(s, TypeRef(name)) == T@STRATEGY(PTs, IT, OT),
    new ss, ss -P-> s,
    paramDefsOk(ss, paramDefs, PTs),
    typeOfExp(ss, inExp) == IT,
    typeOfExp(ss, outExp) == OT,
    @d.type := T.

  declOk(s, d@ClassDecl(name)) :- {T}
    T == CLASS(name),
    declareDecl(s, name, T),
    @d.type := T.


  declOk(s, Decl-Plhdr()).

rules typeOfType : scope * Type -> TYPE
  typesOfTypes maps typeOfType(*, list(*)) = list(*)

  typeOfType(s, t@StrategyType(paramTypes, inputType, outputType)) = T@STRATEGY(PTs, IT, OT) :-
    typesOfTypes(s, paramTypes) == PTs,
    typeOfType(s, inputType) == IT,
    typeOfType(s, outputType) == OT,
    @t.type := T.

  typeOfType(s, t@AnyType()) = T@ANY() :-
    @t.type := T.

  typeOfType(s, t@UnitType()) = T@UNIT() :-
    @t.type := T.

  typeOfType(s, t@BoolType()) = T@BOOL() :-
    @t.type := T.

  typeOfType(s, t@IntType()) = T@INT() :-
    @t.type := T.

  typeOfType(s, t@ListType(type)) = T@LIST(ET) :-
    typeOfType(s, type) == ET,
    @t.type := T.

  typeOfType(s, t@TypeRef(name)) = T :-
    resolveDecl(s, name) == T,
    @t.type := T.

  typeOfType(s, t@TupleType(types)) = T@TUPLE(Ts) :-
    typesOfTypes(s, types) == Ts,
    @t.type := T.

  typeOfType(s, Type-Plhdr()) = _.

//rules typeParamDeclOk : scope * TypeParamDecl
//  typeParamDeclsOk maps typeParamDeclOk(*, list(*))
//
//  // FIXME: This is incorrect because we use the same constraint variable
//  // for all instantiations
//  typeParamDeclOk(s, p@TypeParamDecl(name)) :- {T}
//    // A type parameter, unconstrained
//    declareDecl(s, name, T),
//    @p.type := T.
//
//  typeParamDeclOk(s, TypeParamDecl-Plhdr()).

rules typeOfTypeParamDecl : scope * TypeParamDecl -> TYPE
  typesOfTypeParamDecls maps typeOfTypeParamDecl(*, list(*)) = list(*)

  // This creates a unconstrained type variable T
  typeOfTypeParamDecl(s, p@TypeParamDecl(name)) = T :-
    // A type parameter, unconstrained
    declareDecl(s, name, T),
    @p.type := T.

  typeOfTypeParamDecl(s, TypeParamDecl-Plhdr()) = _.

rules typeOfParamDecl : scope * scope * ParamDecl -> TYPE
  typesOfParamDecls maps typeOfParamDecl(*, *, list(*)) = list(*)

  typeOfParamDecl(s, fs, p@ParamDecl(name, type)) = T :-
    typeOfType(s, type) == T,
    declareDecl(fs, name, T),
    @p.type := T.

rules paramDefOk : scope * ParamDef * TYPE
  paramDefsOk maps paramDefOk(*, list(*), list(*))

  paramDefOk(s, p@ParamDef(name), T) :-
    declareDecl(s, name, T),
    @p.type := T.

  paramDefOk(s, ParamDef-Plhdr(), T).

rules typeOfExp : scope * Exp -> TYPE
  typesOfExps maps typeOfExp(*, list(*)) = list(*)

  typeOfExp(s, e@Seq(e1, e2)) = T :- {T1 T2 IT MT OT}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == STRATEGY([], IT, MT),
    T2 == STRATEGY([], MT, OT),
    T == STRATEGY([], IT, OT),
    @e.type := T.

  typeOfExp(s, e@Id()) = T@STRATEGY([], a, a) :-
    @e.type := T.

  typeOfExp(s, e@Apply(rcv, typeArgs, args)) = T :- {ET TAs PTs IT OT}
    typeOfExp(s, rcv) == ET,
    typesOfTypes(s, typeArgs) == TAs,
    instantiateType(ET, TAs) == STRATEGY(PTs, IT, OT),
    typesOfExps(s, args) == PTs,
    T == STRATEGY([], IT, OT),
    @e.type := T.

//  typeOfExp(s, e@Eval(rcv, input)) = OT :- {IT}
//    typeOfExp(s, rcv) == STRATEGY(IT, OT),
//    typeOfExp(s, input) == IT,
//    @e.type := OT.

  typeOfExp(s, e@Var(name)) = T :-
    resolveDecl(s, name) == T,
    @e.type := T.

  // FIXME: This is incorrect since we want to be able to cast literals to any of the integer types
  typeOfExp(s, e@Int(_)) = T@INT() :-
    @e.type := T.

  typeOfExp(s, e@String(_)) = T@STRING() :-
    @e.type := T.

  typeOfExp(s, e@Object()) = T@ANY() :-
    @e.type := T.

  typeOfExp(s, e@True()) = T@BOOL() :-
    @e.type := T.

  typeOfExp(s, e@False()) = T@BOOL() :-
    @e.type := T.

  // TODO: Accept ANY() as input type
  typeOfExp(s, e@Build(exp)) = T@STRATEGY([], _, TEXP) :-
    typeOfExp(s, exp) == TEXP,
    @e.type := T.

  typeOfExp(s, e@Tuple(exps)) = T@TUPLE(Ts) :-
    typesOfExps(s, exps) == Ts,
    @e.type := T.

  typeOfExp(s, e@LambdaRule(paramDefs, inExp, outExp)) = T@STRATEGY(PTs, IT, OT) :- {ss}
    new ss, ss -P-> s,
    paramDefsOk(ss, paramDefs, PTs),
    typeOfExp(ss, inExp) == IT,
    typeOfExp(ss, outExp) == OT,
    T == STRATEGY(PTs, IT, OT),
    @e.type := T.

  typeOfExp(s, e@LambdaStrategy(paramDefs, exp)) = T@STRATEGY(PTs, IT, OT) :- {ss}
    new ss, ss -P-> s,
    paramDefsOk(ss, paramDefs, PTs),
    typeOfExp(ss, exp) == STRATEGY([], IT, OT),
    T == STRATEGY(PTs, IT, OT),
    @e.type := T.

  typeOfExp(s, Exp-Plhdr()) = T.

rules subtypeOf : TYPE * TYPE

  subtypeOf(T, T).

rules instantiateType : TYPE * list(TYPE) -> TYPE

  // If the type is generic, instantiate it
  instantiateType(GENERIC(TPs, T), TAs) = T :-
    // Each type parameter in TPs is a constraint variable that occurs in T
    // By associating them with the type arguments TAs
    // we instantiate the generic type.
    TPs == TAs.

  // If the type is not generic, the type arguments list must be empty
  instantiateType(T, []) = T.


rules declareModule : scope * string * scope

  declareModule(s, name, ms) :-
    !type[Module{name@name}, MODULE(ms)] in s.

rules declareDecl : scope * string * TYPE

  declareDecl(s, name, T) :-
    !type[Decl{name@name}, T] in s.

rules resolveDecl : scope * string -> TYPE

  resolveDecl(s, name) = T :-
    query type
      filter P*
      and { d' :- d' == Decl{name@_} }
      min $ < P
      in s |-> [(_, (_, T))|_].

signature
  sorts TYPE
  constructors
    MODULE   : scope -> TYPE
    STRATEGY : list(TYPE) * TYPE * TYPE -> TYPE
    CLASS    : string -> TYPE
    LIST     : TYPE -> TYPE
    TUPLE    : list(TYPE) -> TYPE

    GENERIC  : list(TYPE) * TYPE -> TYPE        // The list(TYPE) is a list of constraint variables representing placeholders for types

    BYTE     : TYPE
    SHORT    : TYPE
    INT      : TYPE
    LONG     : TYPE

    UBYTE    : TYPE
    USHORT   : TYPE
    UINT     : TYPE
    ULONG    : TYPE

    ANY      : TYPE
    BOOL     : TYPE
    UNIT     : TYPE
    STRING   : TYPE
  sorts
    DECL = (path * (occurrence * TYPE))

signature
  namespaces
    Module   : ID
    Decl     : ID
  relations
    type     : occurrence -> TYPE
  name-resolution
    labels
      P      // laxical parent
